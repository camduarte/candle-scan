// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © cdu4rte
//@version=5

// @description Herramientas para detectar características en velas japonesas.
library (title = "camdLib")

const int MOMENTUM_LENGTH = 5
const int SESSIONS_LENGTH = 5

//@function Obtiene la longitud del momento.
//@returns La longitud del momento.
export getMomentumLength() =>
    MOMENTUM_LENGTH

//@function Obtiene la longitud de sesiones.
//@returns La longitud de sesiones.
export getSessionsLength() =>
    SESSIONS_LENGTH

//@function Calcula el cuerpo de la vela.
//@returns El tamaño del cuerpo real.
export getBodySize() => 
    math.abs(close - open)

//@function Obtiene el tamaño de la vela.
//@param index El indice de la vela.
//@returns El tamaño del cuerpo real. 
export getBodySize(int index) =>
    math.abs(close[index] - open[index])

//@function Calcula la sombra superior.
//@returns El tamaño de la sombra superior.
export getUpperShadow() => 
    high - (open > close ? open : close)

//@function Calcula la sombra superior
//@param index El índice de la vela.
//@returns El tamaño de la sombra superior.
export getUpperShadow(int index) => 
   high[index] - (open[index] > close[index] ? open[index] : close[index])

//@function Calcula la sombra inferior.
//@returns El tamaño de la sombra inferior.
export getLowerShadow() =>
    (open < close ? open : close) - low

//@function Calcula la sombra inferior
//@param index El índice de la vela.
//@returns El tamaño de la sombra inferior.
export getLowerShadow(int index) =>
    (open[index] < close[index] ? open[index] : close[index]) - low[index]

//@function Determina si la vela es blanca(alcista).
//@param c El precio de cierre.
//@param o El precio de apertura.
//@returns true si la vela es blanca(alcista).
export isWhite(float c, float o) =>
    c > o

//@function Determina si la vela es blanca(alcista).
//@param index El índice de la vela.
//@returns true si la vela es blanca(alcista).
export isWhite(int index) =>
    close[index] > open[index]

//@function Determina si la vela es negra(bajista).
//@param c El precio de cierre.
//@param o El precio de apertura.
//@returns true si la vela es negra(bajista).
export isBlack(float c, float o) =>
    c < o

//@function Determina si la vela es negra(bajista).
//@param index El índice de la vela.
//@returns true si la vela es negra(bajista).
export isBlack(int index) =>
    close[index] < open[index]

//@function Determina si el cierre de la vela está por encima del cierre anterior.
//@param c El precio de cierre de la vela.
//@param c_prev El precio de cierre de la vela anterior.
//@returns true si el cierre de la vela está por encima del cierre anterior.
export isHigherClose(float c, float c_prev) =>
    c > c_prev

//@function Determina si el cierre de la vela está por encima del cierre anterior.
//@param idx1 El índice de la vela de referencia.
//@param idx2 El índice de la vela anterior.
//@returns true si el cierre de la vela está por encima del cierre anterior.
export isHigherClose(int idx1, int idx2) =>
    close[idx1] > close[idx2]

//@function Determina si el cierre de la vela está por debajo del cierre anterior.
//@param c El precio de cierre de la vela.
//@param c_prev El precio de cierre de la vela anterior.
//@returns true si el precio de cierre está por debajo del cierre anterior.
export isLowerClose(float c, float c_prev) =>
    c < c_prev

//@function Determina si el cierre de la vela está por debajo del cierre anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre está por debajo del cierre anterior.
export isLowerClose(int idx1, int idx2) =>
    close[idx1] < close[idx2]

// Calcula el tamaño promedio de las 5 velas anteriores.
//@param index El índice de la vela de referencia.
export getAverageCandleSize(int index) =>
    float result = 0
    for i = 1 to 5
        result += getBodySize(index + i)
    result / 5

//@function Calcula el tamaño promedio de las velas anteriores.
//@param index El índice de la vela.
//@param length Cantidad de sesiones anteriores.
//@returns El tamaño promedio de las velas anteriores.
export getAverageCandleSize(int index, int length) =>
    float result = 0
    for i = 1 to length
        result += getBodySize(index + i)
    result / length

// Determina si el cuerpo de la vela es grande.
//@param index La posicón de la vela de referencia.
// amount cantidad de velas anterioes.
export isBigBody(int index, int amount) =>
    getBodySize(index) > getAverageCandleSize(index, amount)

// Determina si el cuerpo de la vela es pequeña.
// pos el índice de la vela de referencia.
// length Cantidad de velas anterioes.
export isSmallBody(int pos, int length) =>
    getBodySize(pos) < getAverageCandleSize(pos, length)

// Calcula la diferencia entre dos precios y devuelve el resultado sin signo.
// price1 El precio.
// price2 El precio.
// return La diferencia entre los dos precios sin signo.
export calculatePriceDifference(float price1, float price2) =>
    math.abs(price1 - price2)

// Función para verificar si las dos últimas velas muestran signos de agotamiento de la tendencia alcista.
// c1 El precio de cierre de la vela perteneciente a dos períodos previos.
// o1 El precio de apertura de la vela perteneciente a dos períodos previos.
// c2 El precio de cierre de la vela perteneciente a un período previos.
// o2 El precio de apertura de la vela perteneciente a un período previos.
// c3 El precio de cierre de la vela actual.
// o2 El precio de apertura de la vela actual.
export isExhaustionPattern(float c1, float o1, float c2, float o2, float c3, float o3) =>
    // Las sombras superiores son largas o los cuerpos reales son pequeños
    (high - math.max(c2, o2) > 2 * (c2 - o2) and // Sombras superiores largas de la vela 2
     high - math.max(c3, o3) > 2 * (c3 - o3)) or // Sombras superiores largas de la vela 3
     ((c2 - o2) < (c1 - o1) * 0.5 and // El cuerpo real de la vela 2 es pequeño
     (c3 - o3) < (c2 - o2) * 0.5) // El cuerpo real de la vela 3 es pequeño

// Función para verificar si la última vela muestra signo de agotamiento de la tendencia alcista.
// c1 El precio de cierre de la vela anterior.
// o1 El precio de apertura de la vela anterior.
// c2 El precio de cierre de la vela que se desea analizar agotamiento.
// o2 El precio de apertura de la vela que se desea analizar agotamiento.
// h2 El precio máximo de la vela que se desea analizar agotamiento.
export isExhaustionPattern(float c1, float o1, float c2, float o2, float h2) =>
    // Las sombras superiores son largas o los cuerpos reales son pequeños
    h2 - math.max(c2, o2) > 2 * math.abs(c2 - o2) or // Sombras superiores largas de la vela 2
     math.abs(c2 - o2) < math.abs(c1 - o1) * 0.5 // El cuerpo real de la vela 2 es pequeño

// Verifica si existe hueco alcista.
export isBullishGap() =>
    open > high[1]

// Verifica si existe hueco bajista.
export isBearishGap() =>
   open < low[1]

// Verifica si existe hueco bajista respecto a los cuerpos reales.
//@param index El índice de la vela de referencia.
// return true si existe hueco bajista respecto a los cuerpos reales. 
export  isBearishGapRelativeToBodies(int index) =>
   open < math.min(close[index + 1], open[index + 1])

// Verifica si existe hueco alcista respecto a los cuerpos reales.
//@param index El índice de la vela de referencia.
// returns true si existe hueco alcista respecto a los cuerpos reales. 
export isBullishGapRelativeToBodies(int index) =>
   open > math.max(close[index + 1], open[index + 1])

// Determina si existe hueco bajista con respecto a las sombras.
// refCandle El índice de la vela de referencia.
// prevCandle El índice de la vela anterior.
export isBearishGapRelativeToShadows(int refCandle, int prevCandle) =>
   high[refCandle] < low[prevCandle]

// Determina si existe hueco alcista con respecto a las sombras.
// refCandle El índice de la vela de referencia.
// prevCandle El índice de la vela anterior.
export isBullishGapRelativeToShadows(int refCandle, int prevCandle) =>
   low[refCandle] > high[prevCandle]

//@function Determina si existe hueco alcista respecto a los cuerpos reales.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si existe hueco alcista respecto a los cuerpos reales.
export isBullishGapRelativeToBodies(int idx1, int idx2) =>
    math.min(open[idx1], close[idx1]) > math.max(open[idx2], close[idx2])

//@function Determina si existe hueco bajista con respecto a los cuerpos reales.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si existe hueco bajista con respecto a los cuerpos reales.
export isBearishGapRelativeToBodies(int idx1, int idx2) =>
    math.max(open[idx1], close[idx1]) < math.min(open[idx2], close[idx2])

//@function Determina si el cuerpo de la vela está contenida dentro de la vela anterior.
//@param index El índice de la vela.
//@returns true si el cuerpo de la vela está contenida dentro de la vela anterior.
export isBodyInsidePrevBody(int index) =>
    indexPrevCandle = index + 1 
    math.max(open[index], close[index]) < math.max(open[indexPrevCandle], close[indexPrevCandle]) and
     math.min(open[index], close[index]) > math.min(open[indexPrevCandle], close[indexPrevCandle])

//@function Determina si el cuerpo de la vela está contenida dentro de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el cuerpo de la vela está contenida dentro de la vela anterior.
export isBodyInsidePrevBody(int idx1, int idx2) =>
    math.max(open[idx1], close[idx1]) < math.max(open[idx2], close[idx2]) and
     math.min(open[idx1], close[idx1]) > math.min(open[idx2], close[idx2])

// @function Determina si el cuerpo de la vela se encuentra en la zona media-alta de la vela anterior.
// @param index El índice de la vela.
// @returns true si el cuerpo de la vela está dentro de la zona media-alta de la vela anterior.
export isBodyInTheUpperMiddleZone(int index) =>
    indexPrevCandle = index + 1
    halfBody = getBodySize(indexPrevCandle) / 2
    prevPrice = math.max(close[indexPrevCandle], open[indexPrevCandle]) - halfBody
    math.max(close[index], open[index]) >= prevPrice

// Determina si el mínimo de la vela de referencia está por debajo del mínimo de la vela anterior.
// refCandle El índice de la vela de referencia.
export isLowerLow(int refCandle) =>
    prevCandle = refCandle + 1
    low[refCandle] < low[prevCandle]

// Determina si el cuerpo de la vela de referencia está por debajo del cuerpo de la vela previa indicada.
// refCandle El índice de la vela de referencia.
// prevCandle El índice de la vela previa.
export isBodyBelowPrevBody(int refCandle, int prevCandle) =>
    math.max(close[refCandle], open[refCandle]) < math.min(close[prevCandle], open[prevCandle])

// Determina si el cuerpo de la vela de referencia está por encima del cuerpo de la vela previa indicada.
// refCandle El índice de la vela de referencia.
// prevCandle El índice de la vela previa.
export isBodyAbovePrevBody(int refCandle, int prevCandle) =>
    math.min(close[refCandle], open[refCandle]) > math.max(close[prevCandle], open[prevCandle])

// Determina si la mecha inferior es larga.
// pos El índice de la vela.
export isLongShadow(int pos) =>
    getLowerShadow(pos) > getBodySize(pos)

// Determina si la vela tiene sombras.
//@param index El índice de la vela.
export hasShadows(int index) =>
    high[index] != math.max(close[index], open[index]) or
     low[index] != math.min(close[index], open[index])

//@function Determina si la vela posee sombra superior.
//@param index El índice de la vela.
//@returns true si la vela posee vela superior.
export hasUpperShadow(int index) =>
    high > math.max(close[index], open[index])

//@function Determina si la vela posee sombra inferior.
//@param index El índice de la vela.
//@returns true si la vela posee vela inferior.
export hasLowerShadow(int index) =>
    low < math.min(close[index], open[index])

// Determina si el cuerpo de la vela está dentro del rango de la vela anterior indicada.
// refCandle El índice de la vela de referencia.
// prevCandle El índice de la vela anterior.
export isInsidePrevRange(int refCandle, int prevCandle) =>
    math.max(close[refCandle], open[refCandle]) < high[prevCandle] and
     math.min(close[refCandle], open[refCandle]) > low[prevCandle]

//@function Determina si la vela envuelve la vela anterior.
//@param index El índice de la vela.
//@returns true si la vela envuelve la vela anterior.
export isEngulfingBar(int index) =>
    math.max(close[index], open[index]) > math.max(close[index + 1], open[index + 1]) and
     math.min(close[index], open[index]) < math.min(close[index + 1], open[index + 1])

// Determina si la tendencia de corto plazo es alcista.
// @returns true si la tendencia de corto plazo es alcista.
export isBullishTrend() =>
    ta.mom(close, MOMENTUM_LENGTH) > 0

// Determina si la tendencia de corto plazo es bajista.
// retur true si la tendencia de corto plazo es bajista.
export isBearishTrend() =>
    ta.mom(close, MOMENTUM_LENGTH) < 0

// @function Determina si el precio de cierre es cercano al precio mínimo.
// @param index El índice de la vela de referencia.
// @returns true si el precio de cierre está cerca al precio mínimo.
export isCloseNearToLow(int index) =>
    close[index] <= low[index] + (high[index] - low[index]) * 0.05

//@function Determina si la sombra inferior de la vela es corta.
//@param index El índice de la vela.
//@returns true si la vela posee una sombra inferior pequeña.
export hasShortLowerShadow(int index) =>
    getLowerShadow(index) <= getBodySize(index) * 0.025

//@function Determina si el precio de cierre está cerca del precio de cierre de la vela anterior.
//@param idx1 El índice de la vela de referencia.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre está cercano al precio de cierre de la vela anterior.
export isCloseNearToPrevClose(int idx1, int idx2) =>
    candleRange = math.abs(close[idx2] - open[idx2])
    tolerance = candleRange * 0.05
    close[idx1] < close[idx2] + tolerance and close[idx1] > close[idx2] - tolerance

//@function Determina si la apertura de la vela se encuentra dentro del cuerpo real de la vela anterior.
//@param index El índice de la vela.
//@returns true si el precio de apertura de la vela está dentro del cuerpo real de la vela anterior.
export isOpenInsidePrevBody(int index) =>
    open[index] >= math.min(open[index + 1], close[index + 1]) and
     open[index] <= math.max(open[index + 1], close[index + 1])

//@function Determina si la apertura de la vela se encuentra dentro del cuerpo real de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de apertura de la vela está dentro del cuerpo real de la vela anterior.
export isOpenInsidePrevBody(int idx1, int idx2) =>
    open[idx1] >= math.min(open[idx2], close[idx2]) and
     open[idx1] <= math.max(open[idx2], close[idx2])

//@function Determina si el precio de cierre de la vela se encuentra dentro del cuerpo real de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre de la vela se encuentra dentro del cuerpo real de la vela anterior.
export isCloseInsidePrevBody(int idx1, int idx2) =>
    close[idx1] >= math.min(open[idx2], close[idx2]) and
     close[idx1] <= math.max(open[idx2], close[idx2])

// @function Calcula el rango de la vela.
// @param idx1 El índice de la vela.
// @returns El rango de la vela.
export getRange(int index) =>
    math.abs(high[index] - low[index])

//@function Determina si el precio de cierre está por encima del máximo anterior indicado.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el cierre está por encima del máximo anterior indicado.
export isCloseAbovePrevHigh(int idx1, int idx2) =>
    close[idx1] > high[idx2]

//@function Determina si el precio de cierre está por debajo del mínimo anterior indicado.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el cierre está por debajo del mínimo anterior indicado.
export isCloseBelowPrevLow(int idx1, int idx2) =>
    close[idx1] < low[idx2]

//@function Determina si la vela tiene trasero afeitado o es Shaven Bottom.
//@param index El índice de la vela.
//@returns true si la vela tiene el trasero afeitado o es Shaven Bottom.
export hasShavenBottom(int index) =>
    low[index] == math.min(open[index], close[index])

//@function Determina si el precio de apertura está por debajo del mínimo anterior indicado.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de apertura está por debajo del mínimo anterior indicado.
export isOpenBelowPrevLow(int idx1, int idx2) =>
    open[idx1] < low[idx2]

//@function Obtiene el precio medio del cuerpo real de la vela.
//@param index El índice de la vela.
//@return El precio medio del cuerpo real de la vela.
export getBodyMiddlePrice(int index) =>
    math.max(open[index], close[index]) - getBodySize(index) / 2

//@function Determina si el precio de cierre se encuentra entre la parte baja del cuerpo real precedente y el precio medio de dicho cuerpo real.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre se sitúa entre la parte baja del cuerpo real precedente y el precio medio de dicho cuerpo real.
export isCloseInsidePrevLowMiddlePart(int idx1, int idx2) =>
    close[idx1] < getBodyMiddlePrice(idx2) and close[idx1] > math.min(open[idx2], close[idx2])

//@function Determina si el precio de cierre se encuentra cerca de la parte baja del cuerpo real precedente.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre se encuentra cerca de la parte baja del cuerpo real precedente.
export isCloseNearToLowerBodyPart(int idx1, int idx2) =>
    close[idx1] < math.min(open[idx2], close[idx2]) + getBodySize(idx2) * 0.025 and
     close[idx1] > math.min(open[idx2], close[idx2]) - getBodySize(idx2) * 0.025

//@function Determina si el precio de cierre se encuentra por encima del punto medio del cuerpo real de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre se encuentra por encima del punto medio del cuerpo real de la vela anterior.
export isCloseAboveMidpointPrevBody(int idx1, int idx2) =>
    close[idx1] > math.min(open[idx2], close[idx2]) + getBodySize(idx2) / 2

//@function Determina si el precio de cierre se encuentra por debajo del punto medio del cuerpo real de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre se encuentra por debajo del punto medio del cuerpo real de la vela anterior.
export isCloseBelowMidpointPrevBody(int idx1, int idx2) =>
    close[idx1] < math.min(open[idx2], close[idx2]) + getBodySize(idx2) / 2

//@function Determina si el precio de cierre es cercano al precio mínimo de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre se encuentra cerca del precio mínimo de la vela anterior.
export isCloseNearToPrevLow(int idx1, int idx2) =>
    close[idx1] < low[idx2] + getRange(idx2) * 0.025 and
     close[idx1] > low[idx2] - getRange(idx2) * 0.025

//@function Determina si el precio de apertura es próximo al precio de apertura de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de apertura es próximo al precio de apertura de la vela anterior.
export isOpeningCloseToPrevOpening(int idx1, int idx2) =>
    open[idx1] < open[idx2] + getBodySize(idx2) * 0.025 and
     open[idx1] > open[idx2] - getBodySize(idx2) * 0.025

//@function Determina si el precio máximo es más alto que el máximo anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio máximo es más alto que el máximo anterior.
export isHigherHigh(int idx1, int idx2) =>
    high[idx1] > high[idx2]

//@function Determina si el precio de cierre de la vela es más alto que el precio de apertura de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre de la vela es más alto que el precio de apertura de la vela anterior.
export isCloseHigherPrevOpen(int idx1, int idx2) =>
    close[idx1] > open[idx2]

//@function Determina si el precio de apertura de la vela es más alto que el precio de cierre de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de apertura de la vela es más alto que el precio de cierre de la vela anterior.
export isOpenHigherPrevClose(int idx1, int idx2) =>
    open[idx1] > close[idx2]

//@function Determina si el precio de cierre de la vela está por debajo del precio de apertura de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de cierre de la vela está por debajo del precio de apertura de la vela anterior.
export isCloseBelowPrevOpen(int idx1, int idx2) =>
    close[idx1] < open[idx2]

//@function Determina si el precio de apertura de la vela está por debajo del precio de cierre de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true si el precio de apertura de la vela está por debajo del precio de cierre de la vela anterior.
export isOpenBelowPrevClose(int idx1, int idx2) =>
    open[idx1] > close[idx2]

//@function Determina si el rango de la vela es mayor al rango de la vela anterior.
//@param idx1 El índice de la vela.
//@param idx2 El índice de la vela anterior.
//@returns true Determina si el rango de la vela es mayor al rango de la vela anterior.
export isBiggerRange(int idx1, int idx2) =>
    getRange(idx1) > getRange(idx2)
